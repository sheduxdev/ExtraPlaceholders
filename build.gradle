plugins {
    id "java"
    id "java-library"
    id("com.gradleup.shadow") version "${shadowJarVersion}"
}

group = project.findProperty("group") ?: "network.soylu"
version = project.findProperty("version") ?: "v0.1"
description = project.findProperty("description") ?: "Custom PlaceholderAPI expansion"


dependencies {

    compileOnly "org.spigotmc:spigot-api:${project.findProperty("spigotApiVersion")}"
    compileOnly "me.clip:placeholderapi:${project.findProperty("placeholderApiVersion")}"
    compileOnly "xyz.refinedev.practice:BoltAPI:${project.findProperty("boltApiVersion")}"
    compileOnly "xyz.refinedev.phoenix:pxAPI:${project.findProperty("phoenixApiVersion")}"
    compileOnly "org.projectlombok:lombok:${project.findProperty("lombokVersion")}"
    implementation("net.j4c0b3y.CommandAPI:bukkit:${project.findProperty("commandApiVersion")}")
    implementation("net.j4c0b3y:ConfigAPI-core:${project.findProperty("configApiVersion")}")
    annotationProcessor "org.projectlombok:lombok:${project.findProperty("lombokVersion")}"
}

def targetJavaVersion = project.findProperty("targetJavaVersion")?.toInteger() ?: 17

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion

    if (JavaVersion.current() < javaVersion) {
        toolchain {
            languageVersion = JavaLanguageVersion.of(targetJavaVersion)
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = project.findProperty("sourceEncoding") ?: "UTF-8"
    options.compilerArgs << "-parameters"
    options.fork = true

    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17

    if (JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [
            version: version,
            name: project.findProperty("projectName") ?: "ExtraPlaceholders",
            description: description,
            minecraftVersion: project.findProperty("minecraftVersion") ?: "1.8.9",
            apiVersion: project.findProperty("apiVersion") ?: "1.8"
    ]

    inputs.properties(props)
    filteringCharset(project.findProperty("sourceEncoding") ?: "UTF-8")

    filesMatching("plugin.yml") {
        expand(props)
    }
}

shadowJar {
    archiveBaseName.set(project.findProperty("projectName") ?: "ExtraPlaceholders")
    archiveClassifier.set("")
    archiveVersion.set(version.toString())

    manifest {
        attributes(
                "Implementation-Title": project.findProperty("projectName"),
                "Implementation-Version": version,
                "Built-By": System.getProperty("user.name"),
                "Built-JDK": System.getProperty("java.version"),
                "Created-By": "Gradle ${gradle.gradleVersion}"
        )
    }

    minimize()
}

build {
    dependsOn shadowJar
}

clean {
    delete project.findProperty("buildDir") ?: "build"
}